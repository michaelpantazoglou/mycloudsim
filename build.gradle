apply plugin : 'java'
apply plugin : 'idea'
apply plugin : 'eclipse'
apply plugin: 'application'
apply plugin: 'distribution'

task wrapper (type: Wrapper) {
	gradleVersion = '2.0'
}

mainClassName = 'gr.uoa.magdik.cloudsim.Main'
applicationName = 'analyzer'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir 'src/cloudsim'
			srcDir 'src/cloudsim-examples'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
		}
		resources {
			srcDir 'src/test/resources'
		}
	}
}

dependencies {

	compile group: 'org.apache.commons', name: 'commons-math3', version: '3.3'
	
	testCompile group: 'junit', name: 'junit', version: '4.11'	

}

repositories {
	mavenCentral()
	mavenLocal()
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
	manifest {
		attributes 'Implementation-Title': 'Gradle Jar File Example',
				'Implementation-Version': '1',
				'Main-Class': 'gr.uoa.magdik.cloudsim.Main'
	}
	baseName = project.name + '-all'
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

task hyperun (type: JavaExec, dependsOn: classes){
	if(project.hasProperty('myargs')){
		args(myargs.split(','))
	}
	description = "HyperCloudSim"
	main = "gr.uoa.magdik.cloudsim.Main"
	classpath = sourceSets.main.runtimeClasspath
}